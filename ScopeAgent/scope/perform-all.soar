# elaborate to consolidate existing all tasks into one wme that indicates that any exist
sp {elaborate*tasks-exist
   (state <s> ^name scope
              # test for new so that support will
              # retract when perform-all is applied,
              # so we don't get an operator-no-change impasse
              ^io.input-link.task.new)
-->
   (<s> ^tasks-exist yes)
}

# propose perform-all as long as there are any tasks
sp {propose*perform-all
   (state <s> ^name scope
              # as long as any tasks exist
              ^tasks-exist yes
              # test for the strategy-counter so we don't get an infinite loop of elaborations
              ^strategy-counter)
-->
   (<s> ^operator <o> +)
   (<o> ^name perform-all)
}

# send the perform-all strategy to output
sp {apply*perform-all
   (state <s> ^name scope
              ^operator <o>
              ^io.output-link <output>)
   (<o> ^name perform-all)
-->
   (<output> ^strategy <st>)
   (<st> ^name perform-all)
   (write (crlf) |Perform All|)
}