# elaborate to consolidate existing all tasks into one wme that indicates that any exist
sp {elaborate*tasks-exist
   (state <s> ^name scope
              # test for new so that support will
              # retract when perform-all is applied,
              # so we don't get an operator-no-change impasse
              #^io.input-link.task.release)
              ^io.input-link.task <task>)
-->
   (<s> ^tasks-exist yes)
}

# propose perform-all as long as there are any tasks
sp {propose*perform-all
   (state <s> ^name scope
              # as long as any tasks exist
#              ^tasks-exist yes
              ^io.input-link.task <task>
              # test for the strategy-counter so we don't get an infinite loop of elaborations
              ^strategy-counter)
   (<task> ^release yes)
-->
   (<s> ^operator <o> +)
   (<o> ^name perform-all)
}
# all perform-all equal
sp {preference*perform-all*indifferent
   (state <s> ^name scope
              ^operator <o1> +
              ^operator {<o2> <> <o1>} +)
   (<o1> ^name perform-all)
   (<o2> ^name perform-all)
-->
   (<s> ^operator <o1> = <o2>)
}

# elaborate perform-all with the workload that would
# result if we accepted the tasks that it suggests
sp {elaborate*perform-all*workload*release
   (state <s> ^name scope
              ^operator <o> +
              # TODO this assumes only one of these
              ^io.input-link.task <task>
              ^workload <workload>)
   (<task> ^release yes
           ^workload <new-workload>)
   (<o> ^name perform-all)
-->
   (<o> ^workload (+ <workload> <new-workload>))
}
# elaborate perform-all with the workload when
# there are no tasks in release
sp {elaborate*perform-all*workload*no*release
   (state <s> ^name scope
              ^operator <o> +
             -^io.input-link.task.release yes
              ^workload <workload>)
   (<o> ^name perform-all)
-->
   (<o> ^workload <workload>)
}

# send the perform-all strategy to output
sp {apply*perform-all
   (state <s> ^name scope
              ^operator <o>
              ^io.output-link <output>)
   (<o> ^name perform-all)
-->
   (<output> ^strategy <st>)
   (<st> ^name perform-all)
   (write (crlf) |Perform All|)
}