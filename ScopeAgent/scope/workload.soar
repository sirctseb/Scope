# TODO make a subgoal for updating workload instead of just
# making these ops better than strategy ops

# initialize workload values
sp {apply*initialize-workload
   (state <s> ^operator <o>)
   (<o> ^name initialize-scope)
-->
   (<s> ^workload 0)
}

# elaboration to indicate that a workload update is needed
# due to a removed task
sp {elaborate*update-workload*remove
   (state <s> ^name scope
              ^tasks-accounted-for <task>
             -^io.input-link.task <task>)
   # for debug output
   (<task> ^taskID <taskID>)
-->
   (<s> ^workload-update-needed yes)
   # details on update needed
   (<s> ^workload-update <w>)
   (<w> ^task <task>
        ^action remove)
   (write (crlf) |Noticed that task | <taskID> | was gone, need update|)
}
# elaboration to indicate that a workload update is needed
# due to an added task (this also adds workload for resumed tasks)
sp {elaborate*update-workload*add
   (state <s> ^name scope
              ^io.input-link.task (<task> ^active)
             -^tasks-accounted-for <task>)
   (<task> ^active yes)
   # for debug output
   (<task> ^taskID <taskID>)
-->
   (<s> ^workload-update-needed yes)
   # details on update needed
   (<s> ^workload-update <w>)
   (<w> ^task <task>
        ^action add)
   (write (crlf) |Noticed new active task | <taskID> | so we need update|)
}
# elaboration to indicate that a workload update is needed
# due to a delayed task
sp {elaborate*update-workload*delayed
   (state <s> ^name scope
              ^tasks-accounted-for <task>
              ^io.input-link.task (<task> ^delayed yes))
   # for debug output
   (<task> ^taskID <taskID>)
-->
   (<s> ^workload-update-needed yes)
   # details on update neede
   (<s> ^workload-update <w>)
   (<w> ^task <task>
        ^action remove)
   (write (crlf) |Noticed that task | <taskID> | was delayed, need update|)
}

# propose update workload subgoal when a task is added or removed
sp {propose*update-workload-value*subgoal
   (state <s> ^name scope)
   (<s> ^workload-update-needed yes)
-->
   (<s> ^operator <o> + >)
   (<o> ^name update-workload)
}
# make remove-workload-value operators better than any strategy
sp {preference*update*better*than*strategy
   (state <s> ^name scope
              ^operator <o> +
              ^operator <o2> +)
   (<o> ^name update-workload)
   (<o2> ^strategy yes)
-->
   (<s> ^operator <o> > <o2>)
}

# propose update workload operator under update-workload subgoal
sp {propose*workload-update
   (state <s> ^name update-workload
              ^top-state <ts>)
   (<ts> ^workload-update <w>)
-->
   (<s> ^operator <o> + >)
   (<o> ^name update-workload-action
        ^workload-update <w>)
}
# make all workload-update operators equal
sp {preference*update-workload*equal
   (state <s> ^name update-workload
              ^operator <o1> +
              ^operator {<o2> <> <o1>} +)
   (<o1> ^name update-workload-action)
   (<o2> ^name update-workload-action)
-->
   (<s> ^operator <o1> = <o2>)
}
# apply workload update in update-workload subgoal for add
sp {apply*update-workload*add
   (state <s> ^name update-workload
              ^top-state <ts>
              ^operator <o>)
   (<ts> ^workload <workload>)
   (<o> ^name update-workload-action
        ^workload-update <w>)
   (<w> ^task <t>
        ^action add)
   (<t> ^workload <new-workload>)
-->
   (<ts> ^workload <workload> -
         ^workload (+ <workload> <new-workload>)
         ^tasks-accounted-for <t>)
}
# apply workload update in update-workload subgoal for remove
sp {apply*update-workload*remove
   (state <s> ^name update-workload
              ^top-state <ts>
              ^operator <o>)
   (<ts> ^workload <workload>)
   (<o> ^name update-workload-action
        ^workload-update <w>)
   (<w> ^task <t>
        ^action remove)
   (<t> ^workload <new-workload>)
-->
   (<ts> ^workload <workload> -
        ^workload (- <workload> <new-workload>)
        ^tasks-accounted-for <t> -)
}