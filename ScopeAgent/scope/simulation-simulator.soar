# indicate that we are simulating the simulator
sp {simulator*indicator
   (state <s> ^io.input-link <input>)
   (<input> -^IMPRINT)
-->
   (<s> ^simulated yes)
}

# test choosing ignore-new over interrupt-task
# get the first one out of the way
sp {start*simulation*simulator
   (state <s> ^operator <o>
              ^simulated yes
              ^io.input-link <input>)
   (<o> ^name initialize-scope)
-->
   (<s> ^phase simulate-input)
   (<input> ^threshold 8)
}

# alternate a simulated input
sp {propose*simulate-input
   (state <s> ^name scope
              ^phase simulate-input
              ^simulated yes)
-->
   (<s> ^operator <o> + !)
   (<o> ^name simulate-input)
}
# in the application of input simulation,
# change phase back to real scope processing
sp {apply*simulate-input
   (state <s> ^name scope
              #^phase simulate-input
              ^operator <o>
              ^simulated yes)
   (<o> ^name simulate-input)
-->
   (<s> ^phase simulate-input -)
}
# in the application of real scope processing,
# change phase back to input simulation
sp {apply*not-simulate-input
   (state <s> #^name scope
              ^operator <o>
              ^simulated yes
              ^top-state <ts>)
   (<o> -^name simulate-input
         ^strategy yes)
-->
   (<ts> ^phase simulate-input)
}
              
# elaborate operators to indicate that they are strategy operators
# TODO this should not be defined here because it's used in real operation
sp {elaborate*strategy*operators
   (state <s> ^operator <o> +)
   (<o> ^name << perform-all ignore-new interrupt-task
                 delay-new resume-delayed resume-none wait
                 reject-duplicate expire-none >> )
-->
   (<o> ^strategy yes)
}


# initialize strategy counter
# TODO this can't test ^simulated because real rules test ^strategy-counter
sp {apply*initialize-strategy-counter
   (state <s> ^operator <o>
)#              ^simulated yes)
   (<o> ^name initialize-scope)
-->
   (<s> ^strategy-counter 0)
}
# TODO you have to test ^strategy-counter in the
# conditions for every strategy operator proposal or this
# elaborates infinitely. figure out why
# increment a counter every time a strategy decision is made
sp {apply*increment-strategy-counter
   (state <s> #^name scope
              ^operator <o>
              ^strategy-counter <count>
              ^simulated yes)
    (<o> ^strategy yes)
-->
   (<s> ^strategy-counter (+ <count> 1)
        ^strategy-counter <count> -)
}

# remove old commands from output
sp {apply*remove*old*commands
   (state <s> ^simulated yes
              ^operator <o>
              ^io.output-link <output>)
   (<output> ^strategy <strategy>)
   (<strategy> ^complete yes)
   (<o> ^name simulate-input)
-->
   (<output> ^strategy <strategy> -)
}
# simulator effects for output commands
sp {apply*perform-all*effects
   (state <s> ^simulated yes
              ^operator <o>
              ^io.input-link.task <task>
              ^io.output-link.strategy <strategy>)
   (<strategy> ^name perform-all
              -^complete yes)
   (<o> ^name simulate-input)
   (<task> ^release yes)
-->
   # remove release flag
   (<task> ^release yes -)
   # add active flag
   (<task> ^active yes)
   # mark as complete
   (<strategy> ^complete yes)
}

sp {apply*ignore-new*effects
   (state <s> ^simulated yes
              ^operator <o>
              ^io.input-link <input>
              ^io.input-link.task <task>
              ^io.output-link.strategy <strategy>)
   (<strategy> ^name ignore-new
              -^complete yes)
   (<o> ^name simulate-input)
   (<task> ^release yes)
-->
   # remove task from input
   (<input> ^task <task> -)
   # mark as complete
   (<strategy> ^complete yes)
}

sp {apply*interrupt-task*effects
   (state <s> ^simulated yes
              ^operator <o>
              ^io.input-link <input>
              ^io.input-link.task <interrupt-task>
              ^io.input-link.task <new-task>
              ^io.output-link.strategy <strategy>)
   (<o> ^name simulate-input)
   (<strategy> ^name interrupt-task
         ^interrupt-task <interrupt-task>
         ^new-task <new-task>
        -^complete yes)
-->
   # remove release flag from new task
   (<new-task> ^release yes -)
   # add active flag to new task
   (<new-task> ^active yes)
   # remove interrupted task from input
   # TODO this would be easier if it was just flagged
   # and left there.
   # TODO simulate attempts to resume interrupted task
   #(<input> ^task <interrupt-task> -)
   (<interrupt-task> ^active yes -
                     ^delayed yes)
   # store task in special place on top state
   # so that we many resume it some time
#   (<s> ^interrupted-task <interrupt-task>)
   # mark as complete
   (<strategy> ^complete yes)
}

sp {apply*delay-new*effects
   (state <s> ^simulated yes
              ^operator <o>
              ^io.input-link.task <delay-task>
              ^io.output-link.strategy <strategy>)
   (<o> ^name simulate-input)
   (<strategy> ^name delay-new
               ^task <delay-task>
              -^complete yes)
-->
   # remove release flag from new task
   (<delay-task> ^release yes -)
   # add delayed flag to new task
   (<delay-task> ^delayed yes)
   # mark as complete
   (<strategy> ^complete yes)
}

sp {apply*reject-duplicate*effects
   (state <s> ^simulated yes
              ^operator.name simulate-input
              ^io.output-link.strategy (<strategy>
              ^name reject-duplicate
              ^task <task>)
              ^io.input-link <in>)
-->
   # remove rejected from input
   (<in> ^task <task> -)
   # mark as complete
   (<strategy> ^complete yes)
}

sp {apply*resume-delayed*effects
   (state <s> ^simulated yes
              ^operator.name simulate-input
              ^io.output-link.strategy <strategy>)
   (<strategy> ^name resume-delayed
               ^task <task>)
-->
   (<task> ^delayed yes -
           ^active yes)
}

# set which test we want to perform
sp {init*task*events
   (state <s> ^operator <o> +
              ^simulated yes)
   (<o> ^name initialize-scope)
-->
   (<o> ^test-name reject-duplicate)
#   (<o> ^test-name wait)
#   (<o> ^test-name resume-tie)
#   (<o> ^test-name resume-none)
#   (<o> ^test-name tie*interrupt*delay*ignore)
#   (<o> ^test-name default)
#   (<o> ^test-name simultaneous-add-remove)
}

# copy down simulation commands into substates
sp {copy*simulation*state
   (state <s> ^superstate.simulate-task <st>)
-->
   (<s> ^simulate-task <st>)
}

# generalized rules
sp {apply*simulate-input*remove
   (state <s> ^operator.name simulate-input
              ^simulated yes # redundant with above
              ^simulate-task.current <current>)
   (<current> ^remove <removal>)
   (<removal> ^obj <obj>
              ^{<attr> <> <obj>} <value>)
-->
   (<obj> ^<attr> <value> -)
}
sp {apply*simulate-input*add
   (state <s> #^name scope
              ^phase simulate-input
              ^operator <o>
              ^simulated yes
              ^simulate-task.current <current>)
   (<o> ^name simulate-input)
   (<current> ^add <addition>)
   (<addition> ^obj <obj>
               ^{<attr> <> obj} <val>)
-->
   (<obj> ^<attr> <val>)
}
sp {apply*simulate-input*add*task
   (state <s> #^name scope
              ^phase simulate-input
              ^operator <o>
              ^simulated yes
              ^simulate-task.current <current>
              ^io.input-link <input>)
   (<o> ^name simulate-input)
   (<current> ^task <task>)
-->
   (<input> ^task <task>)
}
sp {apply*simulate-input*end
   (state <s> #^name scope
              ^phase simulate-input
              ^operator <o>
              ^simulated yes
              ^simulate-task.current <current>)
   (<current> -^next <next>)
-->
   (write (crlf) |end|)
   (halt)
}
sp {x*finish*simulate*input
   (state <s> #^name scope
              ^phase simulate-input
              ^operator <o>
              ^simulate-task <sim>)
   (<sim> ^current <current>)
   (<current> ^next <next>)
   (<o> ^name simulate-input)
-->
   (<sim> ^current <current> -
        ^current <next>)
}