# propose ignore-new if when a new task is added
# TODO figure out if it will be the case that we never have two new tasks
# at the same time. It may be true because the agent should run after each new
# task starts in the simulation
sp {propose*ignore-new
   (state <s> ^name scope
              ^io.input-link.task <task>
              ^strategy-counter)
   (<task> ^release yes)
-->
   (<s> ^operator <o> +)
   (<o> ^name ignore-new
        ^task <task>)
}

# elaborate ignore-new operators with the workload that would result if
# they were accepted and nothing rejected
sp {elaborate*ignore-new*workload
   (state <s> ^name scope
              ^operator <o> +
              ^workload <workload>)
   (<o> ^name ignore-new
        ^task.workload <new-workload>)
-->
   (<o> ^workload (+ <workload> <new-workload>))
}

# apply ignore-new operator
sp {apply*ignore-new
   (state <s> ^name scope
              ^operator <o>
              ^io.output-link <output>)
   (<o> ^name ignore-new
        ^task <task>)
-->
   (<output> ^strategy <st>)
   (<st> ^name ignore-new
         ^task <task>)
   (write (crlf) |Ignore new task|)
}